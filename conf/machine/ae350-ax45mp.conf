#@TYPE: Machine
#@NAME: ae350-ax45mp
#@SOC: AE350 AX45MP
#@DESCRIPTION: Machine configuration for the AE350 Platfrom (AX45MP)
#@MAINTAINER: Yu-Chine Peter Lin <peterlin@andestech.com>

require conf/machine/include/riscv/tune-riscv.inc

MACHINE_FEATURES = "screen keyboard ext2 ext3 serial"

KERNEL_CLASSES = "kernel-fitimage"
KERNEL_IMAGETYPE = "fitImage"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-mainline"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-ae350"

PREFERRED_VERSION_openocd-native = "riscv"
PREFERRED_VERSION_openocd = "riscv"

EXTRA_IMAGEDEPENDS += "opensbi"
RISCV_SBI_PLAT = "andes/ae350"

## This sets u-boot as the default OpenSBI payload
RISCV_SBI_PAYLOAD ?= "u-boot.bin"
## This will set the kernel as the OpenSBI payload. This is not recommended,
##  you can use U-Boot's MMC loading instead.
# RISCV_SBI_PAYLOAD ?= "${KERNEL_IMAGETYPE}-${MACHINE}.bin"

RISCV_SBI_FDT ?= "ae350_c4_64_d.dtb"

SERIAL_CONSOLES = "38400;ttyS0"

MACHINE_EXTRA_RRECOMMENDS += " kernel-modules"

IMAGE_FSTYPES += "wic.gz ext4"
KERNEL_DEVICETREE ?= "${RISCV_SBI_FDT}"

## Do not update fstab file when using wic images
WIC_CREATE_EXTRA_ARGS ?= "--no-fstab-update"

EXTRA_IMAGEDEPENDS += "u-boot-ae350"
UBOOT_MACHINE = "ae350_rv64_spl_xip_defconfig"

UBOOT_ENTRYPOINT = "0x600000"
UBOOT_DTB_LOADADDRESS = "0x20000000"

## Set this to "mmc-boot" to generate a boot.scr.uimg file which should be included
##  in the boot partition. It will try to load a kernel image by TFTP and if that
##  fails it will fall back to local images in the boot partition.
UBOOT_ENV ?= "tftp-mmc-boot"

# U-Boot SPL
SPL_BINARY ?= "spl/u-boot-spl.bin"

# U-Boot ITB
UBOOT_SUFFIX ?= "itb"

## wic default support
WKS_FILE_DEPENDS ?= " \
    u-boot-ae350 \
    opensbi \
    e2fsprogs-native \
    bmap-tools-native \
"

# boot partition
IMAGE_BOOT_FILES ?= " \
    ${KERNEL_IMAGETYPE} \
    boot.scr.uimg \
    uEnv.txt \
    u-boot.itb \
    u-boot-spl.bin \
"

WKS_FILE ?= "ae350-ax45mp-spl.wks"
