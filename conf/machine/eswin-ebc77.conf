#@TYPE: Machine
#@NAME: ESWIN Computing EBC77
#@SOC: ESWIN Computing EIC7700X
#@DESCRIPTION: Machine configuration for ESWIN Computing EBC77 with vendor Linux kernel
#@MAINTAINER: Marcel Ziswiler <marcel@ziswiler.com>

# Extend the list from riscv-common.inc
MACHINE_FEATURES:append = " bluetooth usb wifi"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append = " bluez5 broadcom-bt-firmware iwd"

require conf/machine/include/riscv/tune-riscv.inc

KERNEL_CLASSES = "kernel"
KERNEL_IMAGETYPE = "Image"

KERNEL_DTB_BASENAME = "eic7700-sbc-a1.dtb"
KERNEL_DEVICETREE ?= "eswin/${KERNEL_DTB_BASENAME}"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-eswin-ebc77-dev"

UBOOT_EXTLINUX = "1"
UBOOT_EXTLINUX_CONSOLE ?= ""
# Latest downstream U-Boot has an additional eswin folder prefix in its
# hard-coded fdtfile
# Work around this by specifying fdtdir without an additional eswin folder
UBOOT_EXTLINUX_FDTDIR ?= "/"
UBOOT_EXTLINUX_KERNEL_ARGS ?= "rootwait"
UBOOT_EXTLINUX_KERNEL_IMAGE ?= "/Image"
UBOOT_EXTLINUX_ROOT ?= "root=PARTUUID=de0b253f-02fb-4d70-ad0e-6742db4a3883"

# There is no upstream U-Boot support for ESWIN EIC770x yet
# We rely on booting downstream U-Boot being present in SPI-NOR flash
# Using QEMU machine for now just to generate uboot-extlinux-config
UBOOT_MACHINE ?= "qemu-riscv64_defconfig"

PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot"

# OpenSBI 1.8 introduced support for ESWIN EIC770x (not really used yet)
RISCV_SBI_PLAT = "generic"

SERIAL_CONSOLES = "115200;ttyS0"

IMAGE_FSTYPES += "wic.xz wic.bmap ext4"

IMAGE_BOOT_FILES = "${KERNEL_IMAGETYPE} \
                    ${KERNEL_DTB_BASENAME};${KERNEL_DEVICETREE} \
                    extlinux.conf;extlinux/extlinux.conf \
"

# Don't include kernels and DTBs in standard images
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:remove = "kernel-image-image"
# Install kernel modules by default
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append = " kernel-modules"

WIC_CREATE_EXTRA_ARGS ?= "--no-fstab-update"
WKS_FILE ?= "eswin-ebc77.wks"
