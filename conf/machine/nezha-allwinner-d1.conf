#@TYPE: Machine
#@NAME: nezha-allwinner-d1
#@SOC: Allwinner D1
#@DESCRIPTION: Machine configuration for Nezha board

require conf/machine/include/riscv/tune-riscv.inc

MACHINE_FEATURES = "screen keyboard ext2 ext3 serial"

KERNEL_CLASSES = "kernel-fitimage"
KERNEL_IMAGETYPE = "fitImage"
UBOOT_ENV ?= "boot"
UBOOT_ENV_SUFFIX = "scr.uimg"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-nezha-dev"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-nezha"

PREFERRED_VERSION_openocd-native = "riscv"
PREFERRED_VERSION_openocd = "riscv"
PREFERRED_VERSION_opensbi = "nezha-1.0"
PREFERRED_VERSION_u-boot-tools = "nezha-2022.01"

EXTRA_IMAGEDEPENDS += "opensbi"
RISCV_SBI_PLAT = "generic"

# Depends on which version of the Nezha board is available, you can choose
# "512m", "1g" or "2g". It is necessary to point proper Device Tree Blob for
# U-Boot, OpenSBI or Linux kernel.
DRAM_SIZE ?= "1g"

## This sets u-boot as the default OpenSBI payload
## It is disabled because caused dependency loop. Allwinner D1 use a TOC1 image
## which has OpenSBI, U-Boot and FDT in it.
#RISCV_SBI_PAYLOAD ?= "u-boot.bin"

# Here you can set the device tree blob used by OpenSBI. Depends on which
# version of the Nezha board is available, you can choose "sun20i-d1-nezha" with
# the suffix "-2g.dtb", "-1g.dtb" or "-512m.dtb".
RISCV_SBI_FDT ?= "sun20i-d1-nezha-${DRAM_SIZE}.dtb"

SERIAL_CONSOLES = "115200;ttyS0"

MACHINE_EXTRA_RRECOMMENDS += " kernel-modules"

IMAGE_FSTYPES += "wic.gz wic.bmap ext4"

# Here you can set the device tree blob used by Linux Kernel. Depends on which
# version of the Nezha board is available, you can choose "sun20i-d1-nezha" with
# the suffix "-2g.dtb", "-1g.dtb" or "-512m.dtb".
KERNEL_DEVICETREE ?= "allwinner/sun20i-d1-nezha-${DRAM_SIZE}.dtb"

## Do not update fstab file when using wic images
WIC_CREATE_EXTRA_ARGS ?= "--no-fstab-update"

EXTRA_IMAGEDEPENDS += "u-boot-nezha"
UBOOT_MACHINE = "nezha_defconfig"


UBOOT_ENTRYPOINT = "0x40200000"
UBOOT_DTB_LOADADDRESS = "0x4FA00000"
UBOOT_DTB = "1"

# Here you can set the device tree blob used by U-Boot. Depends on which
# version of the Nezha board is available, you can choose "sun20i-d1-nezha" with
# the suffix "-2g.dtb", "-1g.dtb" or "-512m.dtb".
UBOOT_DTB_BINARY ?= "sun20i-d1-nezha-${DRAM_SIZE}.dtb"

# Deploy boot0 which is used as SPL
EXTRA_IMAGEDEPENDS += "boot0"

## wic default support
WKS_FILE_DEPENDS ?= " \
    u-boot-nezha \
    opensbi \
    e2fsprogs-native \
    bmap-tools-native \
"

IMAGE_BOOT_FILES ?= " \
    ${KERNEL_IMAGETYPE} \
    ${RISCV_SBI_FDT}  \
    boot.scr.uimg \
    uEnv.txt \
"

WKS_FILE ?= "nezha.wks"
